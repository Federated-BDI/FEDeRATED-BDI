
plugins {
    id 'net.corda.plugins.cordapp' version "$corda_gradle_plugin_version"
    id 'net.corda.plugins.quasar-utils' version "$corda_gradle_plugin_version"
}

apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'net.corda.plugins.quasar-utils'

cordapp {
    targetPlatformVersion corda_platform_version
    minimumPlatformVersion corda_platform_version
    workflow {
        name "Template Flows"
        vendor "Corda Open Source"
        licence "Apache License, Version 2.0"
        versionId 4
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
    test {
        resources {
            srcDir rootProject.file("config/test")
        }
    }
    integrationTest {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/kotlin')
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

configurations.all {
    resolutionStrategy {
        // fail eagerly on version conflict (includes transitive dependencies)
        // e.g. multiple different versions of the same dependency (group and name are equal)
        failOnVersionConflict()

        // prefer modules that are part of this build (multi-project or composite build) over external modules
        preferProjectModules()

        // force certain versions of dependencies (including transitive)
        //  *append new forced modules:
        force "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        force "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
        force "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

        force "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
        force "net.corda:corda-core:$corda_release_version"
        force "net.corda:corda-node-driver:$corda_release_version"
    }
}

dependencies {
    cordapp project(":contracts")

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"

    testImplementation "junit:junit:$junit_version"
    testImplementation "io.mockk:mockk:1.9.3"
    testImplementation "org.testcontainers:testcontainers:1.17.5"
    testImplementation "org.jetbrains.kotlin:kotlin-test"

    // Corda dependencies.
    cordaCompile "net.corda:corda-core:$corda_release_version"
    cordaRuntime "net.corda:corda:$corda_release_version"
    testImplementation "net.corda:corda-node-driver:$corda_release_version"

    // Replacing the below deprecated 'compile' with 'implementation' breaks the cordapp at runtime.
    // Retry at Corda >=5.
    implementation "org.eclipse.rdf4j:rdf4j-rio-turtle:3.7.4"
}

task integrationTest(type: Test, dependsOn: []) {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

test {
    maxHeapSize = "4096m"
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        languageVersion = "1.2"
        apiVersion = "1.2"
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}